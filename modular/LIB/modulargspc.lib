///////////////////////////////////////
version="version modulargspc.lib 0.1 Jun_2022 ";
category="Algebraic geometry";
info="
LIBRARY: modulargspc.lib  modular algorithm with GPI-Space

OVERVIEW:
This library contains SINGULAR procedures for the modular patterns of the Singular/GPI-Space framework.

TYPES:
configToken          general configuration token type
gspcConfigToken      configuration token for GPI-Space
modularConfigToken  configuration token for the transition procedure

PROCEDURES:
configure_gspc()                               generates standard config token for GPI-Space
configure_modular()                           generates standard config token for the transition procedure
generateIOTokenTypes()              register the user-defined types for input and output
gspc_modular(list,list,configToken,configToken)    call the wait_all pattern
";

proc mod_init()
{
  LIB "libSINGULAR-modular-module.so";
  LIB "modstd.lib";
  newstruct("configToken", "string tokentype, def options");
  newstruct("gspcConfigToken", "string tmpdir, string nodefile, int procspernode, string rifstrategy, string loghost, int logport, list addargs, string loghostfile");
  newstruct("modularConfigToken", " string library, string procGenNextPrime, string procCompute, string procLift, string procReconstest, int bal1, int bal2, int nbPrimes");
  newstruct("token", "list fieldnames, list data");
}
 
proc configure_gspc()
"USAGE: configure_gspc()
RETURN: a configToken containing a gspcConfigToken with the standard configuration for GPI-Space
"
{
  gspcConfigToken gc;
  gc.rifstrategy = "ssh";
  configToken ct;
  ct.tokentype = "gspcConfigToken";
  ct.options = gc;
  return(ct);
}

proc configure_modular(configToken gc)
"USAGE: configure_modular()
RETURN: a configToken containing a modularConfigToken with the standard
           configuration for the modular pattern.
"
{
  modularConfigToken pc;
  configToken ct;
  ct.tokentype = "modularConfigToken";
  ct.options = pc;

  //Determine the number of nodes from the nodefile which is the number of line
  string s = read(gc.options.nodefile);
  int i;
  int nb_lines = 0;
  for (i = 1; i<=size(s); i++){
    if (s[i] == newline){
      nb_lines = nb_lines + 1;
    }
  }
  ct.options.library = "modulargp.lib";
  ct.options.procGenNextPrime="genNextPrime";
  ct.options.procLift = "liftt";
  ct.options.procReconstest = "reconstest";
  ct.options.nbPrimes = nb_lines*gc.options.procspernode;
  ct.options.bal1 = 2*nb_lines*gc.options.procspernode;
  ct.options.bal2 = nb_lines*gc.options.procspernode;
  return(ct);
}

 proc gspc_modular(list l,list p, configToken gc, configToken pc)
{
  if ((typeof(gc.options) != "gspcConfigToken") || (typeof(pc.options) != "modularConfigToken"))
  {
    ERROR("wrong config token types");
  }
  if (gc.options.logport != 0) // logging requested
  {
    if (gc.options.loghost == "") // no hostname given, so read from file
    {
      if (gc.options.loghostfile == "")
      {
        ERROR("logging requested, no loghost and no loghostfile given");
      }
      string loghost = read(gc.options.loghostfile);
      if ((loghost == "") || (loghost == newline))
      {
        ERROR("no hostname in loghostfile");
      }
      if (loghost[size(loghost)] == newline) // delete trailing LF
      {
        loghost = loghost[1..(size(loghost)-1)];
      }
      gc.options.loghost = loghost;
    }
    list logopt = list("--log-host", gc.options.loghost, "--log-port", string(gc.options.logport));
    gc.options.addargs = gc.options.addargs + logopt;
  }
  if (system("getenv", "LD_LIBRARY_PATH") != "")
  {
    list ldpathopt = list("--worker-env-copy-variable", "LD_LIBRARY_PATH");
    gc.options.addargs = gc.options.addargs + ldpathopt;
  }
  string tmpdir;
  if (gc.options.tmpdir[1] == "/")
  {
    tmpdir = gc.options.tmpdir;
  }
  else
  {
    tmpdir = system("getenv", "PWD") + "/" + gc.options.tmpdir;
  }
  string loadlib;
  if (pc.options.library[1] == "/")
  {
    loadlib = pc.options.library;
  }
  else
  {
    loadlib = system("getenv", "PWD") + "/" + pc.options.library;
  }
           def sm_result = sggspc_modular(l, p,  tmpdir, gc.options.nodefile, gc.options.procspernode, gc.options.rifstrategy, gc.options.addargs, loadlib,pc.options.procGenNextPrime, pc.options.procCompute, pc.options.procLift, pc.options.procReconstest, pc.options.bal1, pc.options.bal2);
    return(sm_result);
}

proc gspc_modular_std(ideal I, configToken gc, list #) //optional arguments: bal1, bal2, nb_primes
{
  
  token input;
  input.fieldnames = list("generators");
  input.data = list(I);
  list l = list(input);
  configToken pc = configure_modular(gc);
  pc.options.procCompute = "stdmodp";
  //read optional arguments
  if (size(#) > 0)
  {
    //read bal1 value
    if (size(#) == 1 && typeof(#[1]) == "int" )
    {
      pc.options.bal1 = #[1];
    }
    else{ //read bal1 and bal2
      if (size(#) == 2 && typeof(#[1]) == "int" && typeof(#[2])=="int"){
        pc.options.bal1 = #[1];
        pc.options.bal2 = #[2];
      }
      else { //read bal1, bal2 and nbPrimes
        if (size(#) == 3 && typeof(#[1]) =="int" && typeof(#[2]) == "int" && typeof(#[3]) == "int" ){
          pc.options.bal1 = #[1];
          pc.options.bal2 = #[2];
          pc.options.nbPrimes = #[3];
        }
        else{
          ERROR("Wrong optional arguments");
        }
      }
    }
  }
  intvec primeiv = primeList(I, pc.options.nbPrimes);
  int i;
  list primelists;
  token t;
  for (i = 1; i <= pc.options.nbPrimes; i++)
  {
    t.fieldnames = list("int");
    t.data = list(primeiv[i]);
	  primelists[i] = t;
  }
  def re = gspc_modular(l,primelists, gc, pc);
  def originalRing = basering;
  def ringResult = re.r_data;
  setring ringResult; //ring where the result is defined
  def result = re.data[1];
  setring originalRing;
  def result = imap(ringResult,result); //copy the result to the basering
  return(result);
}


